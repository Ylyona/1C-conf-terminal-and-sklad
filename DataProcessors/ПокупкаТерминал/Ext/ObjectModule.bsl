Функция СведенияОВнешнейОбработке () Экспорт
	ПараметрыРегистрации = Новый Структура; 
	МассивНазначений = Новый Массив;
	
	//Указываем документ к которому делаем внешнюю печ. форму
	МассивНазначений.Добавить("Документ.ДокПокупкаТерминалов");
	ПараметрыРегистрации.Вставить("Вид", "ПечатнаяФорма");//может быть - ПечатнаяФорма, ЗаполнениеОбъекта, ДополнительныйОтчет, СозданиеСвязанныхОбъектов... 
	ПараметрыРегистрации.Вставить("Назначение", МассивНазначений); //имя под которым обработка будет зарегестрирована в справочнике внешних обработок
	ПараметрыРегистрации.Вставить("Наименование", "Покупка терминалов");
	ПараметрыРегистрации.Вставить("БезопасныйРежим", ЛОЖЬ);
	ПараметрыРегистрации.Вставить("Версия", "3.0"); 
	ПараметрыРегистрации.Вставить("Информация", "Можно написать любой комментарий"); 
	ТаблицаКоманд = ПолучитьТаблицуКоманд();
	ДобавитьКоманду(ТаблицаКоманд, "Покупка", "Покупка", "ВызовСерверногоМетода", Истина,"ПечатьMXL"); 
	ПараметрыРегистрации.Вставить("Команды", ТаблицаКоманд);
	
	Возврат ПараметрыРегистрации;
КонецФункции

Функция ПолучитьТаблицуКоманд() 
	Команды = Новый ТаблицаЗначений; 
	Команды.Колонки.Добавить("Представление", Новый ОписаниеТипов("Строка"));//как будет выглядеть описание печ.формы для пользователя 
	Команды.Колонки.Добавить("Идентификатор", Новый ОписаниеТипов("Строка")); //имя макета печ.формы 
	Команды.Колонки.Добавить("Использование", Новый ОписаниеТипов("Строка")); //ВызовСерверногоМетода 
	Команды.Колонки.Добавить("ПоказыватьОповещение", Новый ОписаниеТипов("Булево")); 
	Команды.Колонки.Добавить("Модификатор", Новый ОписаниеТипов("Строка")); 
	Возврат Команды; 
КонецФункции 

Процедура ДобавитьКоманду(ТаблицаКоманд, Представление, Идентификатор, Использование, ПоказыватьОповещение = Ложь, Модификатор = "") 
	НоваяКоманда = ТаблицаКоманд.Добавить(); 
	НоваяКоманда.Представление = Представление; 
	НоваяКоманда.Идентификатор = Идентификатор; 
	НоваяКоманда.Использование = Использование; 
	НоваяКоманда.ПоказыватьОповещение = ПоказыватьОповещение; 
	НоваяКоманда.Модификатор = Модификатор; 
КонецПроцедуры

Процедура Печать(МассивОбъектов, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт 
	УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Покупка", "Покупка", СформироватьПечатнуюФорму(МассивОбъектов[0], ОбъектыПечати)); 
КонецПроцедуры // Печать()

функция СформироватьПечатнуюФорму(СсылкаНаОбъект, ОбъектПечати) Экспорт
	
		ТаблДок = новый ТабличныйДокумент;
	МакетОбработки = ПолучитьМакет("МакетПокупкаТерминалов");
	
	ОбластьШапки = МакетОбработки.ПолучитьОбласть("Шапка");
	ОбластьШапки.Параметры.НомерПокупки = СсылкаНаОбъект.Номер;
	ОбластьШапки.Параметры.ДатаПокупки = СсылкаНаОбъект.Дата;
	
	ТаблДок.Вывести(ОбластьШапки);
	
	ОбластьСтроки = МакетОбработки.ПолучитьОбласть("СтрокаТЧ");
	Для каждого  Строка из СсылкаНаОбъект.НовыеТерминалы цикл
		ОбластьСтроки.Параметры.Терминал = Строка.НовыйТерминал;
		ОбластьСтроки.Параметры.СерийныйНомер = Строка.СерийныйНомер;
		ОбластьСтроки.Параметры.Характеристики = Строка.Характеристика;
		ОбластьСтроки.Параметры.Цена = Строка.Цена;
		
		ТаблДок.Вывести(ОбластьСтроки);
	КонецЦикла;
	
	ОбластьПодвала = МакетОбработки.ПолучитьОбласть("Подвал");
	ОбластьПодвала.Параметры.Поставщик = СсылкаНаОбъект.Поставщик;
	ОбластьПодвала.Параметры.Покупатель = СсылкаНаОбъект.Покупатель;
	ОбластьПодвала.Параметры.ОбщееКоличество = СсылкаНаОбъект.КоличествоТерминалов;
	ОбластьПодвала.Параметры.Итого = СсылкаНаОбъект.СуммаДокумента;
	
	ТаблДок.Вывести(ОбластьПодвала);
	
	Возврат ТаблДок;
КонецФункции